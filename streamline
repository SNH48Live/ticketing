#!/usr/bin/env zsh

# A custom postprocess script is supported.

setopt errexit

print_error () print -R $'\e[31m'"Error: $*"$'\e[0m' >&2

run_postprocess=1
while [[ $1 == -* ]]; do
    case $1 in
        -h|--help)
            cat >&2 <<EOF
Usage: $0:t [options]

Options:
    -h, --help
        Print this help and exit.
    -n, --no-postprocess
        Do not run the postprocess script.
EOF
            exit 1
            ;;
        -n|--no-postprocess)
            run_postprocess=
            ;;
        --)
            shift
            break
            ;;
        *)
            print_error "Unknown option ${(q-)1}."
            exit 1
            ;;
    esac
    shift
done


here=$0:A:h
cd $here
mkdir -p images

./screenshot.js
timestamp=$(date +%s)
humantimestamp=$(TZ=Asia/Shanghai date -d @$timestamp +%Y%m%d%H%M%S)
isodatetime=$(TZ=Asia/Shanghai date -d @$timestamp --iso-8601=s)
output=images/screenshot-$humantimestamp.png
convert screenshot.png -resize 50% -font Courier -pointsize 16 -gravity southwest -annotate +10+10 $isodatetime $output
optipng -silent $output
rm screenshot.png
echo $output

[[ -n $run_postprocess && -x postprocess ]] && ./postprocess $output
